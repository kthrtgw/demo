plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'war'
}

def domaResources = ['doma.compile.config', 'META-INF/**/*.sql', 'META-INF/**/*.script']
task copyDomaResources(type: Sync)  {
    from sourceSets.main.resources.srcDirs
    into compileJava.destinationDir
    include domaResources
}

compileJava {
    dependsOn copyDomaResources
    options.encoding = 'UTF-8'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	domaGenRuntime
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
	implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.4.0'
	annotationProcessor 'org.seasar.doma:doma-processor:2.42.0'
    implementation 'org.seasar.doma:doma-core:2.42.0'
	domaGenRuntime 'org.seasar.doma:doma-gen:2.28.0'
    domaGenRuntime 'org.postgresql:postgresql:42.2.16'
}

test {
	useJUnitPlatform()
}

task gen {
    group = 'doma-gen'
    doLast {
        ant.taskdef(resource: 'domagentask.properties',
            classpath: configurations.domaGenRuntime.asPath)
        ant.gen(url: 'jdbc:postgresql://db:5432/dev',
            user: 'dev', 
            password: 'pass',
            templatePrimaryDir: "src/main/resources/config/doma/templates",
            schemaName:'public',
            tableNamePattern:'.*'
        ) {    
            entityConfig(packageName:"com.example.demo.entity",useAccessor:false, useListener:false)
            daoConfig(packageName:"com.example.demo.dao", overwrite:true)
            sqlConfig()
        }
    }
}

task renameEntity(type: Sync) {
    dependsOn gen
    from 'src/main/java/com/example/demo/entity'
    into 'src/main/java/com/example/demo/entity'
    rename { String fileName ->
       fileName.replace('s.java', '.java')
    }
}

task renameDao(type: Sync) {
    dependsOn gen
    dependsOn renameEntity
    from 'src/main/java/com/example/demo/dao'
    into 'src/main/java/com/example/demo/dao'
    rename { String fileName ->
        fileName.replace('sDao.java', 'Dao.java')
    }
}

task doma(type: Sync) {
    group = 'doma-gen'
    dependsOn renameDao
}
